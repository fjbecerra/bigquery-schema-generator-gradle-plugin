buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        maven{
            url 'https://plugins.gradle.org/m2/'
        }

    }

    dependencies {
        classpath "com.github.maiflai:gradle-scalatest:0.22"
        classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
    }
}


allprojects {

    group = 'com.pakius'
    version = '1.0.1-SNAPSHOT'

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java-gradle-plugin'
    apply plugin: "com.gradle.plugin-publish"


    repositories {

        mavenLocal()

        mavenCentral()

    }

    plugins.withType(ScalaPlugin).whenPluginAdded {

        configurations {
            scalaCompilerPlugin
        }

        ext {
            scalaVersion = "2.12.6"
            apacheBeamVersion = "2.11.0"
        }

        dependencies {
            scalaCompilerPlugin "org.scalamacros:paradise_2.12.8:2.1.1"

            compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
            compile group: 'org.scala-lang', name: 'scala-compiler', version: scalaVersion
            testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.4'

        }


        sourceSets {
            main {
                scala {
                    srcDirs = ['src/main/scala']
                }
            }
            test {
                scala {
                    srcDirs = ['src/test/scala']
                }
            }

        }

        tasks.withType(ScalaCompile) {

            scalaCompileOptions.additionalParameters = ["-unchecked", "-deprecation", "-feature", "-Xfatal-warnings", "-encoding", "utf8",
                                                        "-Xplugin:" + configurations.scalaCompilerPlugin.asPath, "-language:implicitConversions",
                                                        "-language:postfixOps",
                                                        //"-Ymacro-debug-lite"
            ]
        }

    }

    plugins.withType(com.github.maiflai.ScalaTestPlugin).whenPluginAdded {


        dependencies {
            testRuntime 'org.pegdown:pegdown:1.6.0'
        }

        test {
            reports {
                junitXml.enabled true
                junitXml.destination file("${project.buildDir}/test-results")
                html.destination file("${project.buildDir}/test-results")
            }
        }
    }
}



